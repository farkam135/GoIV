apply plugin: 'java'
apply plugin: 'application'

repositories {
    mavenCentral()
}

dependencies {
    // for ApplicationDatabaseUpdater
    implementation 'com.google.code.gson:gson:2.8.6'
}

task updateDatabase(type: JavaExec) {
    group 'GoIV'
    description 'update database with GAME_MASTER.json'

    main = "com.kamron.pogoiv.devMethods.gameMasterParser.ApplicationDatabaseUpdater"
    classpath = sourceSets.main.runtimeClasspath
    workingDir = projectDir
    File resParent = file(project(':app').projectDir.getPath() + '/src/main/res/', PathValidation.DIRECTORY)

    doLast {
        File formsXml = file('forms.xml', PathValidation.FILE)
        File integersXml = file('integers.xml', PathValidation.FILE)
        FileCollection databaseFiles = files(formsXml, integersXml)
        File resDir = file(resParent.getPath() + '/values/', PathValidation.DIRECTORY)
        copy {
            from projectDir
            into resDir
            databaseFiles.getFiles().each {File file -> include file.getName()}
        }
        delete databaseFiles
        FileTree namesFiles = fileTree(dir: '.')
        namesFiles.include 'pokemon-*.xml'
        namesFiles.each {
            File file -> copyLanguage(file)
        }
    }

    ext.copyLanguage = { File pokemonFile ->
        String filename = pokemonFile.name
        String language = filename.substring(filename.indexOf('-'), filename.lastIndexOf('.'))
        if (language == '-') {
            language = ''
        }

        File resDir = file(resParent.getPath() + '/values' + language)

        copy {
            from pokemonFile
            into resDir
            rename(filename, 'pokemons.xml')
        }

        delete pokemonFile
    }
}